import SDL_Types
import SDL_Primitives
import SDL_Events
embed
#include <SDL2/SDL.h>
#include <SDL2/SDL_ttf.h>
#include <SDL2/SDL_image.h>
#include <stdbool.h>
#include <stdint.h>
#include <stdlib.h>
#include <unistd.h>

typedef   long double real;

typedef struct {
  unsigned char r;
  unsigned char g;
  unsigned char b;
  unsigned char derp;
}pixel;

typedef struct{
  int width;
  int height;
  SDL_Renderer* renderer;
  SDL_Window* window;
  SDL_Event* event;
}sdl_object;

typedef struct{
  int width;
  int height;
  SDL_Texture* texture;
  pixel* pixels;
}sdl_texture;


body
end

-- This is the main class for the SDL library. All handling and running
-- is done, and must only be done within this object. SDL is not threadsafe
-- at all and as such it must be contained within a single active object.
--
-- Drawing is done by first constructing the object.
-- Preferably clear the object.
-- Call some drawing method.
-- Call the refresh method.
--
-- The refresh method must be called for the drawings 
-- to be be displayed to the screen.
--
-- To get the events call the pollEvent or waitForEvent methods
-- these will pass along the event to the supplied event handler.
class SDL_Main
  sdl_settings : embed sdl_object* end
  sdl_texture : embed sdl_texture* end
  sdl_font : SDL_Font
  quit : bool

  --Draws the outline of a rectangle onto the screen.
  def drawRectangle(rec: Rectangle) :void{
    let sdl_settings = this.sdl_settings;
    let sdl_texture = this.sdl_texture;
    let x  = rec.x;
    let y  = rec.y;
    let w  = rec.w;
    let h  = rec.h;
    embed void
      SDL_Rect rect;
      rect.x = #{x};
      rect.y = #{y};
      rect.w = #{w};
      rect.h = #{h};
      SDL_RenderDrawRect(#{sdl_settings}->renderer, &rect);
    end
  }

  -- Fills a rectangle.
  def fillRectangle(rec: Rectangle) :void{
    let sdl_settings = this.sdl_settings;
    let sdl_texture = this.sdl_texture;
    let x  = rec.x;
    let y  = rec.y;
    let w  = rec.w;
    let h  = rec.h;
    embed void
      SDL_Rect rect;
      rect.x = #{x};
      rect.y = #{y};
      rect.w = #{w};
      rect.h = #{h};
      SDL_RenderFillRect(#{sdl_settings}->renderer, &rect);
    end
  }

  -- Sets the color for the renderer.
  -- All objects (not counting text and images) will be 
  -- drawed with this color untill this method is called again.
  def setRenderColor(c: ColorRGBA) : void{
    let sdl_settings = this.sdl_settings;
    let sdl_texture = this.sdl_texture;
    let r = c.r;
    let g = c.g;
    let b = c.b;
    let a = c.a;
    embed void
      SDL_SetRenderDrawColor(#{sdl_settings}->renderer,
                             #{r},
                             #{g},
                             #{b},
                             #{a});
    end
  }

  -- Draws an image onto the screen.
  -- The x and y parameters specifies the upper left corner of the image.
  -- The x_scale and y_scale parameters are used to scale the image.
  def drawImage(image_path : String, x : int, y : int, x_scale : real, y_scale : real): void{
    let sdl_settings = this.sdl_settings;
    let image_path = image_path.data;
    embed void
      SDL_Texture *img = NULL;
      int w, h; 
      img = IMG_LoadTexture(#{sdl_settings}->renderer, #{image_path});
      SDL_QueryTexture(img, NULL, NULL, &w, &h);
      SDL_Rect rect; 
      rect.x = #{x}; 
      rect.y = #{y}; 
      rect.w = (int)(w*#{x_scale}); 
      rect.h = (int)(h*#{y_scale}); 
      SDL_RenderCopy(#{sdl_settings}->renderer, img, NULL, &rect);
      SDL_DestroyTexture(img);
    end
  }


  --Draws a line between the two points.
  def drawLine(p1: Point2D, p2: Point2D) : void{
    let sdl_settings = this.sdl_settings;
    let sdl_texture = this.sdl_texture;
    let x1 = p1.x;
    let y1 = p1.y;
    let x2 = p2.x;
    let y2 = p2.y;
    embed void
      SDL_RenderDrawLine(#{sdl_settings}->renderer,
                         #{x1},
                         #{y1},
                         #{x2},
                         #{y2});
    end
  }

  -- Clears the entire window (fills it with black.)
  def clearAll() : void {
    let sdl_settings = this.sdl_settings;
    embed void
      SDL_SetRenderDrawColor(#{sdl_settings}->renderer,
                             0,
                             0,
                             0,
                             255);
      SDL_RenderClear(#{sdl_settings}->renderer);
    end
  }

  -- Fills the enitre window with the current color.
  def fillAll() : void {
    let sdl_settings = this.sdl_settings;
    embed void
      SDL_RenderClear(#{sdl_settings}->renderer);
    end
  }

  -- Initializes the SDL font.
  -- As of now a limitation of the library is that 
  -- it can only load a single font at a time.
  -- if you want to change fonts during the execution of 
  -- the program you will need to call this funtion every time you want to change the font.
  def initFont(font_name : String, size: int) : void{
    let font = this.sdl_font;
    let font_path = font_name.data;
    embed void
      #{font} = TTF_OpenFont(#{font_path}, #{size});
      if (#{font} == NULL) {
          fprintf(stderr, "error: font not found\n");
          exit(EXIT_FAILURE);
      }
    end;
    this.sdl_font = font;
  }

  -- Draws the text on the screen in the given color.
  -- Will use the font and size loaded with the initFont method.
  def drawText(text: String, x : int, y: int, color: ColorRGBA) : void{ 
    let text_color = color.getCRepresentation();
    let sdl_settings = this.sdl_settings;
    let font = this.sdl_font;
    let c_text = text.data;
    embed void
      int text_width;
      int text_height;
      SDL_Surface *surface;
      SDL_Rect rect;
      SDL_Color textColor = {255, 255, 255, 0};

      surface = TTF_RenderText_Solid(#{font}, #{c_text}, #{text_color});
      SDL_Texture *texture = SDL_CreateTextureFromSurface(#{sdl_settings}->renderer, surface);
      text_width = surface->w;
      text_height = surface->h;
      SDL_FreeSurface(surface);
      rect.x = #{x};
      rect.y = #{y};
      rect.w = text_width;
      rect.h = text_height;
      SDL_RenderCopy(#{sdl_settings}->renderer, texture, NULL, &rect);
      SDL_DestroyTexture(texture);
    end
  }

  -- This constructor will create the SDL_object and open a window.
  -- The width and height will be the width and height of the window.
  def init(width : int, height : int) : void{
    this.quit = false;
    let sdl_settings = this.sdl_settings;
    let sdl_texture = this.sdl_texture;
    embed void 
      SDL_Init(SDL_INIT_VIDEO);
      TTF_Init();
      sdl_object* sdl_settings = (sdl_object*)malloc(sizeof(sdl_object));
      sdl_settings->width = #{width};
      sdl_settings->height = #{height};
      sdl_settings->window = SDL_CreateWindow("Encore Program",
            SDL_WINDOWPOS_UNDEFINED, SDL_WINDOWPOS_UNDEFINED, #{width}, #{height}, 0);
      sdl_settings->renderer = SDL_CreateRenderer(sdl_settings->window, -1, 0); 
      sdl_settings->event = malloc(sizeof(SDL_Event));
      #{sdl_settings} = sdl_settings;

      sdl_texture* sdl_only_texture = malloc(sizeof(sdl_texture));
      sdl_only_texture->width = #{width};
      sdl_only_texture->height = #{height};
      sdl_only_texture->texture =  SDL_CreateTexture(sdl_settings->renderer,
            SDL_PIXELFORMAT_RGB888, SDL_TEXTUREACCESS_STATIC, #{width}, #{height});
      sdl_only_texture->pixels = malloc(sdl_only_texture->width*sdl_only_texture->height*sizeof(pixel));

      #{sdl_texture} = sdl_only_texture;
      
    end;
    this.sdl_settings = sdl_settings;
    this.sdl_texture = sdl_texture;
  }

  def getHeight() : int{
    let sdl_settings = this.sdl_settings;
    embed int #{sdl_settings}->height; end;
  }

  def getWidth() : int{
    let sdl_settings = this.sdl_settings;
    embed int #{sdl_settings}->width; end;
  }


  -- Refreshes the screen.
  -- Objects drawn will not be showed untill this method has been called.
  def refresh() : void {
    let sdl_settings = this.sdl_settings;
    let sdl_texture = this.sdl_texture;
    let quit = this.quit;
    embed void
      SDL_RenderPresent(#{sdl_settings}->renderer);
    end;
  }

  -- Creates a "dumb" copy of the SDL_Event union.
  -- This is necessary since just passing along the pointer to the
  -- event object would cause race conditions.
  def event_clone() : SDL_Event{
    let sdl_settings = this.sdl_settings; 
    embed SDL_Event
      SDL_Event* copy = malloc(sizeof(SDL_Event));
      (SDL_Event*)memcpy(copy,#{sdl_settings}->event,sizeof(SDL_Event));
      copy;
    end
  }

  -- Checks if an event is present and send the event to the event handler
  -- if such an event is present.
  def pollEvent(handler: SDL_Event_Handler) : void{
    let sdl_settings = this.sdl_settings; 
    let event_present = 0;
    embed void
      #{event_present} = SDL_PollEvent(#{sdl_settings}->event);
    end;
    if(event_present == 1) then {
      handler!handleEvent(this, this.event_clone());
    }
  } 

  -- Blocks untill an event is avaliable. The timeout parameters is in milliseconds
  -- and if it is set to zero the method will block untill indefintley utill there
  -- is an event present.
  --
  -- WARNING: There is some confusion as to how this method behaves in the case when it
  -- times even though no new event was found. 
  def waitForEvent(handler: SDL_Event_Handler, timeout : int) : void{
    let sdl_settings = this.sdl_settings; 
    if(timeout == 0) then {
      embed void
        SDL_WaitEvent(#{sdl_settings}->event);
      end
    }else{
      embed void
        SDL_WaitEventTimeout(#{sdl_settings}->event,#{timeout});
      end
    };
    handler!handleEvent(this, this.event_clone());
  }

  def quit() : void{
    embed void
        SDL_Quit();
        10;
    end
  }

--Hack to workaround typechecking bug.
typedef Hack<t> = (SDL_Main,t)

-- This is a Vat wrapper used to store the event actions.
class Event_Vat<event_type>
  vat : Vat<(SDL_Main,event_type)>

  def init() : void{
    this.vat = new Vat<(SDL_Main,event_type)>();
  }

  def registerCallback(action : (Hack<event_type>) -> bool) : void {
    this.vat.registerPost(
      \(a: Maybe (SDL_Main,event_type), b: (SDL_Main,event_type)) -> action(b)
    )
  }

  def pushEvent(event: (SDL_Main,event_type)) : void {
    this.vat!write(event);
  }
  
-- This class handles the events generated by the SDL_PollEvent and
-- the SDL_WaitForEvent.
-- Note:
-- since the event union returned by the SDL code in the Main object
-- gets copied and holds no references to anything the event handling
-- can be handled by a sepparate object.
class SDL_Event_Handler
  mouse_button_vat : Event_Vat<SDL_MouseButtonEvent>
  mouse_motion_vat : Event_Vat<SDL_MouseMotionEvent>
  keyboard_vat : Event_Vat<SDL_KeyboardEvent>

  def init() : void {
    this.mouse_button_vat = new Event_Vat<SDL_MouseButtonEvent>();
    this.keyboard_vat = new Event_Vat<SDL_KeyboardEvent>();
    this.mouse_motion_vat = new Event_Vat<SDL_MouseMotionEvent>();
  }

  --Ads a new callback to be triggered ona mouse button event.
  def addMouseButtonCallback(action : Hack<SDL_MouseButtonEvent> -> bool) : void{
    this.mouse_button_vat.registerCallback(action);
  }
  
  --Ads a new callback to be triggered ona keyboard event
  def addKeyboardCallback(action : Hack<SDL_KeyboardEvent>-> bool) : void{
    this.keyboard_vat.registerCallback(action);
  }
  
  --Ads a new callback to be triggered ona mouse motion event.
  def addMotionCallback(action : Hack<SDL_MouseMotionEvent>-> bool) : void{
    this.mouse_motion_vat.registerCallback(action);
  }

  -- Handle an event.
  -- Note that the event object is just a "basic" copy the SDL_Event
  -- union. Therefore it will not be traced and must be freed manually.
  def handleEvent(main: SDL_Main, event : SDL_Event) : void{
    let event_type = embed int #{event}->type; end;
    if(event_type == embed int SDL_QUIT; end) then{
      embed void exit(0); end;
    }else if (event_type == embed int SDL_MOUSEBUTTONDOWN; end) then{
      this.mouse_button_vat!pushEvent((main,new SDL_MouseButtonEvent(event)));
    }else if (event_type == embed int SDL_MOUSEBUTTONUP; end) then{
      this.mouse_button_vat!pushEvent((main,new SDL_MouseButtonEvent(event)));
    }else if (event_type == embed int SDL_KEYDOWN; end) then{
      this.keyboard_vat!pushEvent((main,new SDL_KeyboardEvent(event)));
    }else if (event_type == embed int SDL_KEYUP; end) then{
      this.keyboard_vat!pushEvent((main,new SDL_KeyboardEvent(event)));
    }else if (event_type == embed int SDL_MOUSEMOTION; end) then{
      this.mouse_motion_vat!pushEvent((main,new SDL_MouseMotionEvent(event)));
    };
    embed void
      //The copied event should be unused by now
      free(#{event});
    end
  }


